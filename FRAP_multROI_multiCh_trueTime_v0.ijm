//Reading out the true time points of acquisition from images of ome.tif
//Measurements is performed on the cirle ROI generated by:
//Projector point ROI - enlarged by 10 pixels - fit to a circle ROI
//Created on 2021-08-06, tested on 2021-08-06
//Contact SoYeon Kim for questions: soyeon.kim@ucsf.edu

//Clean-up the space
print("\\Clear");
run("Close All");

//Comanand for enabling Bio-Formats functions
run("Bio-Formats Macro Extensions");
imageFile = File.openDialog("Choose an image file to analyze"); //Choose the file to open
Ext.setId(imageFile); //Initialize the given id of the image file
Ext.openImagePlus(imageFile); //Open the file
fileDir = getDirectory("image"); //Get the directory from the current image

print(imageFile);
//print (fileDir);

//Report the TrueTimePoints written by PengCheng Zhang (pengcheng.zhang@ucsf.edu)
imageName = getTitle();
Ext.getSizeC(sizeC); //Get the number of channels
Ext.getSizeT(sizeT); //Get the number of time points

//Iterate across each timepoint and channel written by PengCheng Zhang (pengcheng.zhang@ucsf.edu)
for (j=0; j<sizeT; j++) {
	for (i=0; i<sizeC; i++) {
		Ext.getPlaneTimingDeltaT(t, j*sizeC+i);
		setResult(i, j, t);
	}
}

updateResults();

//Make a ROI around the bleachign spot and measure
run("Select All"); //Clearning the ROI manager
roiManager("Add"); //Clearning the ROI manager
roiManager("Deselect"); //Clearning the ROI manager
roiManager("Delete"); //Clearning the ROI manager
roiManager("Open", fileDir + "ProjectorROIs.zip"); //Add the projector ROI

//Choose the ROI
stat = true;

while (stat==true) {

	measureFRAP(imageName);

	Dialog.create("FRAP data measurement");
	Dialog.addMessage("Want to measure another ROI? Click cancel if the measurement is done for the image");
	Dialog.show();

}

function measureFRAP(imageName){
	selectWindow(imageName);
	waitForUser("Choose the ROI to analyze!");
	run("Enlarge...", "enlarge=10 pixel"); //Enlarge the projector ROI by 10 pixels
	run("Fit Circle"); //Make a fit circle ROI

	selectWindow(imageName);
	run("Plot Z-axis Profile"); //Plotting
	plotWindow = getTitle();
	print(plotWindow);

	//run("Show All");

	waitForUser("Gather the intensity data from the plot!");

	selectWindow(plotWindow);
	close();
}
